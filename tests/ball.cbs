function main()
#初始化和设置
	system.println("Console Pinball Game")
	system.println("Please set the difficulty.\nThe smaller your Settings,game play will be more difficult.")
	define jump_frame=system.input()
	darwin.load()
	system.println("Please set the \"up\" key.Default key is 'A'.Press enter to skip.")
	define key_up=darwin.get_kb_hit()
	if(key_up=='\n'||key_up=='\r')
		key_up='a'
	end
	system.println("Please set the \"down\" key.Default key is 'Z'.Press enter to skip.")
	define key_down=darwin.get_kb_hit()
	if(key_down=='\n'||key_down=='\r')
		key_down='z'
	end
	define num3={1,1,1,1,1,1,0,0,0,0,1,1,1,1,1,1,1,1,0,0,0,0,1,1,1,1,1,1,1,1}
	define num2={1,1,1,1,1,1,0,0,0,0,1,1,1,1,1,1,1,1,1,1,0,0,0,0,1,1,1,1,1,1}
	define num1={0,0,1,1,0,0,0,0,1,1,0,0,0,0,1,1,0,0,0,0,1,1,0,0,0,0,1,1,0,0}
	define chG={1,1,1,1,1,1,1,1,0,0,0,0,1,1,0,1,1,1,1,1,0,0,1,1,1,1,1,1,1,1}
	define chO={1,1,1,1,1,1,1,1,0,0,1,1,1,1,0,0,1,1,1,1,0,0,1,1,1,1,1,1,1,1}
	define chExc={0,1,1,1,1,0,0,1,1,1,1,0,0,0,1,1,0,0,0,0,0,0,0,0,0,0,1,1,0,0}
#显示开始的倒计时
	darwin.set_frame_limit(1)
	darwin.fit_drawable()
	function draw_ch(ch,w,h,x,y)
		define r=0
		while(r<h)
			define c=0
			while(c<w)
				if(ch[r*6+c]==1)
					darwin.draw_point(c+x,r+y,darwin.pixel(' ',true,false,darwin.red,darwin.red))
				end
				++c
			end
			++r
		end
	end
	darwin.update_drawable()
	darwin.fill_drawable(darwin.pixel(' ',true,false, darwin.white,darwin.white))
	draw_ch(num3,6,5,0.5*darwin.get_width()-3,0.5*darwin.get_height()-2.5)
	darwin.update_drawable()
	darwin.fill_drawable(darwin.pixel(' ',true,false, darwin.white,darwin.white))
	draw_ch(num2,6,5,0.5*darwin.get_width()-3,0.5*darwin.get_height()-2.5)
	darwin.update_drawable()
	darwin.fill_drawable(darwin.pixel(' ',true,false, darwin.white,darwin.white))
	draw_ch(num1,6,5,0.5*darwin.get_width()-3,0.5*darwin.get_height()-2.5)
	darwin.update_drawable()
	darwin.fill_drawable(darwin.pixel(' ',true,false, darwin.white,darwin.white))
	draw_ch(chG,6,5,0.5*darwin.get_width()-10,0.5*darwin.get_height()-2.5)
	draw_ch(chO,6,5,0.5*darwin.get_width()-2,0.5*darwin.get_height()-2.5)
	draw_ch(chExc,6,5,0.5*darwin.get_width()+5,0.5*darwin.get_height()-2.5)
	darwin.update_drawable()
#主循环
	darwin.set_frame_limit(60)
	define score=0
	define zkh=0.5*darwin.get_height()
	define x=2
	define y=0
	define z1=0.5*darwin.get_height()-0.5*zkh
	define z2=z1
	define xj=true
	define yj=true
	define delay_frame=0
	loop
		darwin.fill_drawable(darwin.pixel(' ',true,false,darwin.white,darwin.blue))
		darwin.draw_line(0,z1,0,z1+zkh,darwin.pixel('#',true,false,darwin.cyan,darwin.cyan))
		darwin.draw_line(darwin.get_width()-1,z2,darwin.get_width()-1,z2+zkh,darwin.pixel('#',true,false,darwin.pink,darwin.pink))
		if(delay_frame>jump_frame)
			delay_frame=0
			if(x<=1)
				if(y<z1||y>z1+zkh)
					break
				end
				xj=true
				x=1
				++score
			end
			if(y<=0)
				yj=true
				y=0
			end
			if(x>=darwin.get_width()-2)
				if(y<z2||y>z2+zkh)
					break
				end
				xj=false
				x=darwin.get_width()-2
				++score
			end
			if(y>=darwin.get_height()-1)
				yj=false
				y=darwin.get_height()-1
			end
			if(xj)
				++x
			else
				--x
			end
			if(yj)
				++y
			else
				--y
			end
		else
			++delay_frame
		end
		if(darwin.is_kb_hit())
			define in=darwin.get_kb_hit()
			if(in==key_up)
				if(xj)
					if(z2>0)
						--z2
					end
				else
					if(z1>0)
						--z1
					end
				end
			end
			if(in==key_down)
				if(xj)
					if(z2+zkh<darwin.get_height()-1)
						++z2
					end
				else
					if(z1+zkh<darwin.get_height()-1)
						++z1
					end
				end
			end
		end
		darwin.draw_point(x,y,darwin.pixel('@',true,false,darwin.red,darwin.yellow))
		define info=to_string(score)
		info.cut(7)
		info="Score:"+info
		darwin.draw_string(0,0,info,darwin.pixel(' ',true,false,darwin.white,darwin.black))
		darwin.update_drawable()
	end
#结束后处理得分
	define str=to_string(score)
	str.cut(7)
	system.println("You Die!Your Score is ",str,".")
	if(score<5)
		system.println("Very Bad.Just try lower difficulty.")
	end
	if(score>=5&&score<10)
		system.println("Not Bad.You can get higher score.")
	end
	if(score>=10&&score<50)
		system.println("Good.")
	end
	if(score>=50&&score<100)
		system.println("Very Great!")
	end
	if(score>=100)
		system.println("So Amazing!")
	end
end
main()
